subprojects {
    dependencies {
        testImplementation libs.test.junit
        testImplementation libs.test.junit.params
        testRuntimeOnly libs.test.junit.engine
        testRuntimeOnly libs.test.junit.platform.launcher

        testImplementation libs.test.mockito.inline
        testImplementation libs.test.assertj

        testImplementation libs.test.lombok
        testAnnotationProcessor libs.test.lombok
    }

    test {
        systemProperty("cucumber.publish.quiet", true)
        useJUnitPlatform {
            excludeTags("disabled")
        }

        reports {
            html.required.set(true)
            html.outputLocation.set(file("${rootProject.buildDir}/tests/${project.name}"))

            junitXml.required.set(true)
            junitXml.outputLocation.set(file("${rootProject.buildDir}/tests/${project.name}"))
        }
    }

    def configCucumber = { name, glues ->
        if (project.name == name) {
            dependencies {
                testImplementation libs.test.cucumber
            }

            configurations {
                cucumberRuntime {
                    extendsFrom testImplementation
                }
            }

            def cucumberArgs = ['--plugin', 'pretty']
            for (glue in glues) {
                cucumberArgs.add('--glue')
                cucumberArgs.add(glue)
            }
            cucumberArgs.add(file("src/test/resources/features").absolutePath)
            tasks.register('cucumber', JavaExec) {
                dependsOn assemble, testClasses
                environment('CUCUMBER_PUBLISH_QUIET', true)
                mainClass = "io.cucumber.core.cli.Main"

                classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output

                args = cucumberArgs
                standardInput = System.in
            }
        }
    }

    if (rootProject.hasProperty('cucumberProjects')) {
        rootProject.cucumberProjects.each { entry ->
            configCucumber(entry.name, entry.glues)
        }
    }
}

project(":DAL-extension-inspector") {
    tasks.named("test") {
        mustRunAfter(":page-flow:test")
    }
}

project(":page-flow") {
    tasks.named("test") {
        mustRunAfter(":page-flow-playwright:test")
        mustRunAfter(":page-flow-selenium:test")
    }
}
